#!/usr/bin/perl
# $Id: piconv,v 1.0 2002/03/28 23:26:28 dankogai Exp dankogai $
#
use 5.7.3;
use strict;
use Encode ;
use Encode::Alias;
my %Scheme =  map {$_ => 1} qw(from_to decode_encode perlio);

use Getopt::Std;

my %Opt; getopts("DS:lf:t:s:", \%Opt);
$Opt{l} and list_encodings();
my $locale = $ENV{LC_CTYPE} || $ENV{LC_ALL} || $ENV{LANG};
my $from = $Opt{f} || $locale or help("from_encoding unspecified");
my $to   = $Opt{t} || $locale or help("to_encoding unspecified");
$Opt{s} and Encode::from_to($Opt{s}, $from, $to) and print $Opt{s} and exit;
my $scheme = exists $Scheme{$Opt{S}} ? $Opt{S} :  'from_to';

if ($Opt{D}){
    my $cfrom = Encode->getEncoding($from)->name;
    my $cto   = Encode->getEncoding($to)->name;
    print STDERR <<"EOT";
Scheme: $scheme
From:   $from => $cfrom
To:     $to => $cto
EOT
}

# default
if     ($scheme eq 'from_to'){ 
    while(<>){
	Encode::from_to($_, $from, $to); print;
    };
# step-by-step
}elsif ($scheme eq 'decode_encode'){
   while(<>){
       my $decoded = decode($from, $_);
       my $encoded = encode($to, $decoded);
       print $encoded;
    };
# NI-S favorite
}elsif ($scheme eq 'perlio'){ 
    binmode(STDIN,  ":encoding($from)");
    binmode(STDOUT, ":encoding($to)");
    while(<>){ print; }
}else{ # won't reach
    die "unknown scheme: $scheme";
}

sub list_encodings{
    print STDERR join("\n", Encode->encodings(":all")), "\n";
    exit;
}

sub help{
    my $message = shift;
    use File::Basename;
    my $name = basename($0);
    $message and print STDERR "$name error: $message\n";
    print STDERR <<"EOT";
$name [-f from_encoding] [-t to_encoding] [-s string] [files...]
$name -l
  -l lists all available encodings.
  -f from_encoding  When omitted, the current locale will be used.
  -t to_encoding    When omitted, the current locale will be used.
  -s string         "string" will be converted instead of STDIN.
EOT
  exit;
}

__END__

=head1 NAME

piconv -- iconv(1), reinvented in perl

=head1 SYNOPSIS

  piconv [-f from_encoding] [-t to_encoding] [-s string] [files...]
  piconv -l

=head1 DESCRIPTION

B<piconv> is perl version of F<iconv>, a character encoding converter
widely availabe for various unixen today.   This script was primarily
a technology demostrator for Perl 5.8.0, you can use piconv in the
place of iconv for virtually any cases.

piconv converts character encoding of either STDIN or files specified
in the argument and prints out to STDOUT.

Here are list of options.

=over 4

=item -f from_encoding

Specifies the encoding you are converting from.  Unlike F<iconv>,
this option can be ommited.  In such cases the current locale is used.

=item -t to_encoding

Specifies the encoding you are converting to.  Unlike F<iconv>,
this option can be ommited.  In such cases the current locale is used.

Therefore when both -f and -t are omitted, F<piconv> just acts like F<cat>.

=item -s I<string>

uses I<string> instead of file for the source of text.  Same as F<iconv>.

=item -l

Lists all available encodings to STDERR.  This feature is missing from
F<iconv>.

=item -D

Invokes debugging mode.  primarily for Encode hackers.

=item -S scheme

Selects which scheme is to be used for conversion.  Available schemes
are as follows;

=over 4

=item from_to

Uses Encode::from_to for conversion.  This is the default.

=item decode_encode

Input strings are decode()ed then encode()ed.  A straight step-by-step
implementation.

=item perlio

The new perlIO layer is used.  NI-S favorite.

=back

Like I<-D> option, this is also for Encode hackers.

=back

=head1 SEE ALSO

L<iconv(1)>
L<locale(3)>
L<Encode>
L<PerlIO>

=cut
