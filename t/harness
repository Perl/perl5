#!./perl

# We suppose that perl _mostly_ works at this moment, so may use
# sophisticated testing.

BEGIN {
    chdir 't' if -d 't';
    @INC = '../lib';              # pick up only this build's lib
}

my $torture; # torture testing?

use TAP::Harness 3.13;
use strict;
use Config;

$::do_nothing = $::do_nothing = 1;
require './TEST';
our $Valgrind_Log;

my $Verbose = 0;
$Verbose++ while @ARGV && $ARGV[0] eq '-v' && shift;

# For valgrind summary output
my $htoolnm;
my $hgrind_ct;

if ($ARGV[0] && $ARGV[0] eq '-torture') {
    shift;
    $torture = 1;
}

# Let tests know they're running in the perl core.  Useful for modules
# which live dual lives on CPAN.
$ENV{PERL_CORE} = 1;

my (@tests, @re, @anti_re);

# [.VMS]TEST.COM calls harness with empty arguments, so clean-up @ARGV
@ARGV = grep $_ && length( $_ ) => @ARGV;

sub _extract_tests;
sub _extract_tests {
    # This can probably be done more tersely with a map, but I doubt that it
    # would be as clear
    my @results;
    foreach (@_) {
	my $ref = ref $_;
	if ($ref) {
	    if ($ref eq 'ARRAY') {
		push @results, _extract_tests @$_;
	    } elsif ($ref eq 'HASH') {
		push @results, _extract_tests values %$_;
	    } else {
		die "Unknown reference type $ref";
	    }
	} else {
	    push @results, glob $_;
	}
    }
    @results;
}

while ($ARGV[0] && $ARGV[0]=~/^-(n?)re/) {
    my $ary= $1 ? \@anti_re : \@re;

    if ( $ARGV[0] !~ /=/ ) {
        shift @ARGV;
        while (@ARGV and $ARGV[0] !~ /^-/) {
            push @$ary, shift @ARGV;
        }
    } else {
        push @$ary, (split/=/,shift @ARGV)[1];
    }
}

my $jobs = $ENV{TEST_JOBS};
my ($rules, $state, $color);
if ($ENV{HARNESS_OPTIONS}) {
    for my $opt ( split /:/, $ENV{HARNESS_OPTIONS} ) {
        if ( $opt =~ /^j(\d*)$/ ) {
            $jobs ||= $1 || 9;
        }
        elsif ( $opt eq 'c' ) {
            $color = 1;
        }
        else {
            die "Unknown HARNESS_OPTIONS item: $opt\n";
        }
    }
}

if (@ARGV) {
    # If you want these run in speed order, just use prove

    # Note: we use glob even on *nix and not just on Windows
    # because arguments might be passed in via the TEST_ARGS
    # env var where they wont be expanded by the shell.
    @tests = map(glob($_),@ARGV);
    # This is a hack to force config_heavy.pl to be loaded, before the
    # prep work for running a test changes directory.
    1 if $Config{d_fork};
} else {
    # Ideally we'd get somewhere close to Tux's Oslo rules
    # my $rules = {
    #     par => [
    #         { seq => '../ext/DB_File/t/*' },
    #         { seq => '../ext/IO_Compress_Zlib/t/*' },
    #         { seq => '../lib/ExtUtils/t/*' },
    #         '*'
    #     ]
    # };

    # but for now, run all directories in sequence.

    unless (@tests) {
	my @seq = <base/*.t>;

        my @last;
	my @next = qw(comp run cmd);

        # The remaining core tests are either intermixed with the non-core for
        # more parallelism (if PERL_TEST_HARNESS_ASAP is set non-zero) or done
        # after the above basic sanity tests, before any non-core ones.
        my $which = $ENV{PERL_TEST_HARNESS_ASAP} ? \@last : \@next;

        push @$which, qw(io re opbasic op uni mro lib porting perf);
	push @$which, 'japh' if $torture;
	push @$which, 'win32' if $^O eq 'MSWin32';
	push @$which, 'benchmark' if $ENV{PERL_BENCHMARK};
	push @$which, 'bigmem' if $ENV{PERL_TEST_MEMORY};

	# Hopefully TAP::Parser::Scheduler will support this syntax soon.
	# my $next = { par => '{' . join (',', @next) . '}/*.t' };
	my $next = { par => [
			     map { "$_/*.t" } @next
			    ] };
	@tests = _extract_tests ($next);

	my $last = { par => '{' . join (',', @last) . '}/*.t' };
	@last = _extract_tests ($last);

	# This is a bit of a game, because we only want to sort these tests in
	# speed order. base/*.t wants to run first, and ext,lib etc last and in
	# MANIFEST order
	if ($jobs) {
	    require App::Prove::State;
	    $state = App::Prove::State->new({ store => 'test_state' });
	    $state->apply_switch('slow', 'save');
	    # For some reason get_tests returns *all* the tests previously run,
	    # (in the right order), not simply the selection in @tests
	    # (in the right order). Not sure if this is a bug or a feature.
	    # Whatever, *we* are only interested in the ones that are in @tests
	    my %seen;
	    @seen{@tests} = ();
	    @tests = grep {exists $seen{$_} } $state->get_tests(0, @tests);
	}
	@tests = (@seq, @tests);
	push @seq, $next;

	push @last,
	    _tests_from_manifest($Config{extensions}, $Config{known_extensions});
	my %times;
	if ($state) {
	    # Where known, collate the elapsed times by test name
	    foreach ($state->results->tests()) {
		$times{$_->name} = $_->elapsed();
	    }
	}

	my %dir;
	my %total_time;
        my %serials;
        my %all_dirs;

        # Preprocess the list of tests
	for (@last) {
	    if ($^O eq 'MSWin32') {
		s,\\,/,g; # canonicalize path
	    };

            # Keep a list of the distinct directory names, and another list of
            # those which contain a file whose name begins with a 0
            if ( m! \A (?: \.\. / )?
                                ( .*? )         # $1 is the directory path name
                            /
                                ( [^/]* \.t )   # $2 is the .t name
                    \z !x)
            {
                my $path = $1;

                $all_dirs{$path} = 1;
                $serials{$path} = 1 if $2 =~ / \A 0 /x;
            }
        }

        # We assume that the reason a test file's name begins with a 0 is to
        # order its execution among the tests in its directory.  Hence, a
        # directory containing such files should be tested in serial order.
        #
        # Add exceptions to the above rule
        for (qw(cpan/IO-Zlib/t ext/File-Find/t)) {
            $serials{$_} = 1;
        }

        my @nonexistent_serials = grep { not exists $all_dirs{$_} } keys %serials;
        if (@nonexistent_serials) {
            die "These directories to be run serially don't exist."
              . "  Check your spelling:\n" . join "\n", @nonexistent_serials;
        }

        # Remove the serial testing directories from the list of all
        # directories.  The remaining ones are testable in parallel.  Make the
        # parallel list a scalar with names separated by '|' so that below
        # they will be added to a regular expression.
        my $non_serials = join "|", grep { not exists $serials{$_} } keys %all_dirs;
        undef %all_dirs;
        undef %serials;

	for (@last) {
            # Treat every file in each non-serial directory as its own
            # "directory", so that it can be executed in parallel
            m! \A ( (?: \.\. / )? (?: $non_serials )
                         / [^/]+ \.t \z | .* [/] ) !x
                or die "'$_'";
	    push @{$dir{$1}}, $_;

            # This file contributes time to the total needed for the directory
            # as a whole
	    $total_time{$1} += $times{$_} || 0;
	}
        #print STDERR __LINE__, join "\n", sort { $total_time{$b} <=> $total_time{$a} } keys %dir, "  ";

	push @tests, @last;

	# Generate T::H schedule rules that run the contents of each directory
	# sequentially.
	push @seq, { par => [ map { s!/$!/*!; { seq => $_ } } sort {
	    # Directories, ordered by total time descending then name ascending
	    $total_time{$b} <=> $total_time{$a} || lc $a cmp lc $b
	} keys %dir ] };

	$rules = { seq => \@seq };
    }
}
if ($^O eq 'MSWin32') {
    s,\\,/,g for @tests;
}
if (@re or @anti_re) {
    my @keepers;
    foreach my $test (@tests) {
        my $keep = 0;
        if (@re) {
            foreach my $re (@re) {
                $keep = 1 if $test=~/$re/;
            }
        } else {
            $keep = 1;
        }
        if (@anti_re) {
            foreach my $anti_re (@anti_re) {
                $keep = 0 if $test=~/$anti_re/;
            }
        }
        if ($keep) {
            push @keepers, $test;
        }
    }
    @tests= @keepers;
}

# Allow eg ./perl t/harness t/op/lc.t
for (@tests) {
    if (! -f $_ && !/^\.\./ && -f "../$_") {
        $_ = "../$_";
        s{^\.\./t/}{};
    }
}

my %options;

my $type = 'perl';

# Load TAP::Parser now as otherwise it could be required in the short time span
# in which the harness process chdirs into ext/Dist
require TAP::Parser;

my $h = TAP::Harness->new({
    rules       => $rules,
    color       => $color,
    jobs        => $jobs,
    verbosity   => $Verbose,
    timer       => $ENV{HARNESS_TIMER},
    exec        => sub {
	my ($harness, $test) = @_;

	my $options = $options{$test};
	if (!defined $options) {
	    $options = $options{$test} = _scan_test($test, $type);
	}

	(local $Valgrind_Log = "$test.valgrind-current") =~ s/^.*\///;

	return [ split ' ', _cmd($options, $type) ];
    },
});

# Print valgrind output after test completes
if ($ENV{PERL_VALGRIND}) {
    $h->callback(
		 after_test => sub {
		     my ($job) = @_;
		     my $test = $job->[0];
		     my $vfile = "$test.valgrind-current";
	             $vfile =~ s/^.*\///;

		     if ( (! -z $vfile) && open(my $voutput, '<', $vfile)) {
			print "$test: Valgrind output:\n";
			print "$test: $_" for <$voutput>;
			close($voutput);
		     }

		     (local $Valgrind_Log = "$test.valgrind-current") =~ s/^.*\///;

		     _check_valgrind(\$htoolnm, \$hgrind_ct, \$test);
		 }
		 );
}

if ($state) {
    $h->callback(
		 after_test => sub {
		     $state->observe_test(@_);
		 }
		 );
    $h->callback(
		 after_runtests => sub {
		     $state->commit(@_);
		 }
		 );
}

$h->callback(
	     parser_args => sub {
		 my ($args, $job) = @_;
		 my $test = $job->[0];
		 _before_fork($options{$test});
		 push @{ $args->{switches} }, "-I../../lib";
	     }
	     );

$h->callback(
	     made_parser => sub {
		 my ($parser, $job) = @_;
		 my $test = $job->[0];
		 my $options = delete $options{$test};
		 _after_fork($options);
	     }
	     );

my $agg = $h->runtests(@tests);
_cleanup_valgrind(\$htoolnm, \$hgrind_ct);
exit $agg->has_errors ? 1 : 0;
